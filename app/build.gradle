apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.shlomikatriel.expensesmanager"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        def languageCodes = ["en", "iw"]
        resConfigs languageCodes

        buildConfigField "String[]", "LANGUAGE_CODES", "{${languageCodes.collect { "\"$it\"" }.join(",")}}"
        buildConfigField "String", "LOG_TAG", '"BucksBunny"'
        buildConfigField "int", "MAX_MONTHS_OFFSET", "${rootProject.ext.maxMonthsOffset}"
        buildConfigField "int", "MONTHS_COUNT", "${rootProject.ext.maxMonthsOffset * 2 + 1}"
        buildConfigField "String", "SHARED_PREFERNCES_FILE_NAME", '"shared_preferences"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions.annotationProcessorOptions {
            arguments += [
                    "room.incremental"   : "true",
                    "room.schemaLocation": "$projectDir/schemas".toString()
            ]
        }
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    signingConfigs {
        release {
            storeFile file(rootProject.ext.storeFilePath)
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            versionNameSuffix '-debug'
        }
    }

    buildFeatures {
        dataBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.1'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.room:room-testing:2.3.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Logging
    implementation 'com.github.bosphere.android-filelogger:filelogger:1.0.7'
    implementation 'net.lingala.zip4j:zip4j:2.9.0'

    // Room
    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Open Source Licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:19.0.0'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.1'

    // Play Core
    implementation 'com.google.android.play:core:1.10.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // Compose
    implementation 'androidx.compose.ui:ui:1.0.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.0.1'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.0.1'
    // Material Design
    implementation 'androidx.compose.material:material:1.0.1'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.0.1'
    implementation 'androidx.compose.material:material-icons-extended:1.0.1'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    implementation 'androidx.compose.runtime:runtime-livedata:1.0.1'
    // View Pager
    implementation 'com.google.accompanist:accompanist-pager:0.16.1'
    implementation "com.google.accompanist:accompanist-pager-indicators:0.16.1"
}