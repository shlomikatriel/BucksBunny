apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.shlomikatriel.expensesmanager"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        def languageCodes = ["en", "iw"]
        resConfigs languageCodes
        buildConfigField "String[]", "LANGUAGE_CODES", "{${languageCodes.collect { "\"$it\"" }.join(",")}}"

        def countryCodes = ["AU", "US", "GB", "IL", "CA"]
        buildConfigField "String[]", "COUNTRY_CODES", "{${countryCodes.collect { "\"$it\"" }.join(",")}}"

        buildConfigField "String", "LOG_TAG", '"BucksBunny"'
        buildConfigField "int", "MONTHS_COUNT", "12"
        buildConfigField "String", "SHARED_PREFERNCES_FILE_NAME", '"shared_preferences"'
        buildConfigField "String", "MAIL_ADDRESS", '"shlomikatriel@gmail.com"'
        buildConfigField "String", "GITHUB_URL", '"https://github.com/shlomikatriel/ExpensesManager"'
        buildConfigField "String", "APACHE_LICENSE_URL", '"https://raw.githubusercontent.com/shlomikatriel/ExpensesManager/develop/LICENSE"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions.annotationProcessorOptions {
            arguments += [
                    "room.incremental"   : "true",
                    "room.schemaLocation": "$projectDir/schemas".toString()
            ]
        }
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    signingConfigs {
        release {
            storeFile file(rootProject.ext.storeFilePath)
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            versionNameSuffix '-debug'
        }
    }

    buildFeatures {
        dataBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    namespace 'com.shlomikatriel.expensesmanager'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    // TODO move to 1.6.0 when stable
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.6.0-rc01'
    implementation 'com.google.android.material:material:1.7.0'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.room:room-testing:2.4.3"
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    // Logging
    implementation 'net.lingala.zip4j:zip4j:2.9.0'

    // Room
    implementation "androidx.room:room-runtime:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

    // Open Source Licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:21.2.0'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.3.2'

    // Play Core
    implementation 'com.google.android.play:review-ktx:2.0.1'
    implementation 'com.google.android.play:app-update-ktx:2.0.1'

    // Compose
    implementation 'androidx.compose.ui:ui:1.3.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.3.1'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.3.1'
    // Material Design
    implementation "androidx.compose.material3:material3:1.0.1"
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.3.1'
    implementation 'androidx.compose.material:material-icons-extended:1.3.1'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.6.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.3.1'
    // View Pager
    implementation "com.google.accompanist:accompanist-pager:0.25.1"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.25.1"
}