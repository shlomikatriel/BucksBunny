apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.shlomikatriel.expensesmanager"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        resConfigs "en", "iw"

        buildConfigField "String", "LOG_TAG", '"ExpensesManager"'
        buildConfigField "int", "MAX_MONTHS_OFFSET", "${rootProject.ext.maxMonthsOffset}"
        buildConfigField "int", "MONTHS_COUNT", "${rootProject.ext.maxMonthsOffset * 2 + 1}"
        buildConfigField "String", "SHARED_PREFERNCES_FILE_NAME", '"shared_preferences"'
        buildConfigField "int", "CHIP_FILTER_THRESHOLD", "200"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions.annotationProcessorOptions {
            arguments += [
                    "room.incremental":"true",
                    "room.schemaLocation":"$projectDir/schemas".toString()
            ]
        }
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    signingConfigs {
        release {
            storeFile file(rootProject.ext.storeFilePath)
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix '-debug'
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation "androidx.room:room-testing:2.2.5"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    def versions = rootProject.ext.versions

    // Logging
    implementation 'com.github.bosphere.android-filelogger:filelogger:1.0.7'
    implementation 'net.lingala.zip4j:zip4j:2.6.2'

    // Room
    implementation "androidx.room:room-runtime:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"

    // Dagger
    implementation "com.google.dagger:dagger:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Open Source Licenses
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.0'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx:17.3.0'
}